' This script is an example of the EMDO101 energy manager
' Please visit us at www.swissembedded.com
' Copyright (c) 2015-2016 swissEmbedded GmbH, All rights reserved.
' Eastron SDM630 energy meter EMDO modbus example
' Documentation somewhere in the internet
' @DESCRIPTION EMDO Eastron SDM630 energy meter library
' @VERSION 1.0
LIBRARY LOAD "modbus"

'Some examples on usage:
'LIBRARY UNLOAD "modbus"
'SYS.Set "rs485", "baud=9600 data=8 stop=1 parity=n"
'slv%=1
'itf$="RTU:RS485:1"
'itf$="TCP:192.168.0.250:502"
'start:
' err%=EastronEnergyMeter(itf$,slv%, Uac1, Uac2, Uac3, Iac1, Iac2, Iac3, kW1, kW2, kW3, kWhI1, kWhI2, kWhI3, kWhE1, kWhE2, kWhE3)
' print "Eastron " err% Uac1 Uac2 Uac3 Iac1 Iac2 Iac3 kW1 kW2 kW3 kWhI1 kWhI2 kWhI3 kWhE1 kWhE2 kWhE3
' pause 30000
' goto start

' Eastron energy meter SDM630
' itf$ modbus interface (see EMDO modbus library for details)
' slv% eastron energy meter sdm630 slave address default 1 
' Uac1-3 voltage ac
' Iac1-3 current ac
' kW1-3 up to three phase power with sign, negative = excess power to grid
' kWhI1-3 up to three phase energy import
' kWhE1-3 up to three phase energy export
' return value error code
FUNCTION EastronEnergyMeter(itf$, slv%, Uac1, Uac2, Uac3, Iac1, Iac2, Iac3, kW1, kW2, kW3, kWhI1, kWhI2, kWhI3, kWhE1, kWhE2, kWhE3)
 LOCAL err%, rUac$, rIac$, rkW$, rkWhI$, rkWhE$
 
 err%= mbFunc(itf$,slv%,4,0,6,rUac$,500) OR mbFunc(itf$,slv%,4,6,6,rIac$,500) OR mbFunc(itf$,slv%,4,&HC,6,rkW$,500) OR mbFunc(itf$,slv%,4,&H15A,6,rkWhI$,500) OR mbFunc(itf$,slv%,4,&H160,6,rkWhE$,500)
 IF err% THEN
  EastronEnergyMeter=err%
  EXIT FUNCTION
 ENDIF 
 ' Convert register values to float32
 Uac1=conv("bbe/f32",mid$(rUac$,1,4))
 Uac2=conv("bbe/f32",mid$(rUac$,5,4))
 Uac3=conv("bbe/f32",mid$(rUac$,9,4)) 
 Iac1=conv("bbe/f32",mid$(rIac$,1,4))
 Iac2=conv("bbe/f32",mid$(rIac$,5,4))
 Iac3=conv("bbe/f32",mid$(rIac$,9,4)) 
 kW1=conv("bbe/f32",mid$(rkW$,1,4))/1000.0
 kW2=conv("bbe/f32",mid$(rkW$,5,4))/1000.0
 kW3=conv("bbe/f32",mid$(rkW$,9,4)) /1000.0
 kWhI1=conv("bbe/f32",mid$(rkWhI$,1,4))
 kWhI2=conv("bbe/f32",mid$(rkWhI$,5,4))
 kWhI3=conv("bbe/f32",mid$(rkWhI$,9,4)) 
 kWhE1=conv("bbe/f32",mid$(rkWhE$,1,4))
 kWhE2=conv("bbe/f32",mid$(rkWhE$,5,4))
 kWhE3=conv("bbe/f32",mid$(rkWhE$,9,4)) 
 EastronEnergyMeter=0
END FUNCTION

